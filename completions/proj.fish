# Completions for proj function.
# Helper functions live in functions/__proj_*.fish so fish autoload picks them up
# regardless of whether `proj` or `wt` is being completed first.
complete -c proj -f

# Subcommands
complete -c proj -n '__fish_use_subcommand' -a clone -d 'Clone a repo into base/'
complete -c proj -n '__fish_use_subcommand' -a new -d 'Create empty project with git init'
complete -c proj -n '__fish_use_subcommand' -a convert -d 'Convert old-style to base/ layout'
complete -c proj -n '__fish_use_subcommand' -a hooks -d 'Install git hooks to current project'
complete -c proj -n '__fish_use_subcommand' -a gc -d 'Aggressive git gc (destructive: drops reflog + unreachable objects)'
complete -c proj -n '__fish_use_subcommand' -a ls -d 'List projects'
complete -c proj -n '__fish_use_subcommand' -a wt -d 'Worktree management'

# Project names as cd shortcuts
complete -c proj -n '__fish_use_subcommand' -a '(set -l root (__fish_weft_workspace_root); and test -d "$root"; and for d in $root/*/; basename $d; end)'

# proj wt subcommands
complete -c proj -n '__proj_seen_wt; and not __proj_seen_wt_subcommand' -a add -d 'Create worktree (tracks remote if exists)'
complete -c proj -n '__proj_seen_wt; and not __proj_seen_wt_subcommand' -a fork -d 'Fork from current worktree'
complete -c proj -n '__proj_seen_wt; and not __proj_seen_wt_subcommand' -a sync -d 'Merge wt-parent (or @{upstream}, or whitelist) into current worktree'
complete -c proj -n '__proj_seen_wt; and not __proj_seen_wt_subcommand' -a push -d 'Push current branch to origin'
complete -c proj -n '__proj_seen_wt; and not __proj_seen_wt_subcommand' -a ls -d 'List worktrees'
complete -c proj -n '__proj_seen_wt; and not __proj_seen_wt_subcommand' -a status -d 'Show worktrees with PR status'
complete -c proj -n '__proj_seen_wt; and not __proj_seen_wt_subcommand' -a rm -d 'Remove worktree'
complete -c proj -n '__proj_seen_wt; and not __proj_seen_wt_subcommand' -a clean -d 'Remove merged + stale worktrees'
complete -c proj -n '__proj_seen_wt; and not __proj_seen_wt_subcommand' -a fix-claude-links -d 'Migrate worktrees to symlinked .claude/ + CLAUDE.md'

# proj wt <name> — worktree names as cd shortcuts
complete -c proj -n '__proj_seen_wt; and not __proj_seen_wt_subcommand' -a '(__proj_worktree_names)'

# proj wt add: complete with remote branches
complete -c proj -n '__proj_seen_wt_add' -a '(__proj_remote_branches)'
complete -c proj -n '__proj_seen_wt_add' -l from -d 'Base branch'

# proj wt fork: no special completions (new branch name is user-provided)

# proj wt rm: complete with worktree names and -f flag
complete -c proj -n '__proj_seen_wt_rm' -a '(__proj_worktree_names)'
complete -c proj -n '__proj_seen_wt_rm' -s f -l force -d 'Force remove worktree and delete unmerged branch'

# proj wt clean: --age flag
complete -c proj -n '__proj_seen_wt_clean' -l age -d 'Stale-worktree threshold in days (default 30, 0 disables)'

# proj gc: --yes flag
complete -c proj -n '__proj_seen_gc' -s y -l yes -d 'Skip interactive confirmation'

# proj wt fix-claude-links: --apply flag and project names as positional arg
complete -c proj -n '__proj_seen_wt_fix_claude_links' -l apply -d 'Perform changes (default is dry-run)'
complete -c proj -n '__proj_seen_wt_fix_claude_links' -a '(set -l root (__fish_weft_workspace_root); and test -d "$root"; and for d in $root/*/; basename $d; end)'
